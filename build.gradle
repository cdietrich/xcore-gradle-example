plugins {
    id "org.xtext.builder" version "1.0.21"
}

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'application'

group = 'io.github.ghillairet'
version = '0.4.0-SNAPSHOT'
sourceCompatibility = 1.8

ext {
    xtextVersion = '2.14.0'
}

sourceSets {
    main {
        resources {
            exclude '**/*.xcore'
        }
    }
}

xtext {
    version = "${xtextVersion}"
    languages {
        ecore {
            setup = 'org.eclipse.xtext.ecore.EcoreSupport'
        }
        codegen {
            setup = 'org.eclipse.emf.codegen.ecore.xtext.GenModelSupport'
        }
        xcore {
            setup = 'org.eclipse.emf.ecore.xcore.XcoreStandaloneSetup'
            generator.outlet.producesJava = true
        }
    }
   sourceSets {
      main {
        srcDir 'src/main/resources'
        output {
            dir(xtext.languages.xcore.generator.outlet, 'build/someSpecialDir')
        }
      }
    }
}

dependencies {
    compile "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
    compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
    compile 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'

    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore:+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:+'
    xtextLanguages "org.eclipse.xtext:org.eclipse.xtext.ecore:${xtextVersion}"
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.mwe2.runtime:+'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations.all {
    resolutionStrategy { 
        force 'com.google.guava:guava:21.0'
        force 'org.eclipse.platform:org.eclipse.equinox.common:3.9.0'
        force 'org.eclipse.platform:org.eclipse.equinox.registry:3.7.0'
        force 'org.eclipse.platform:org.eclipse.osgi:3.12.100'
        force 'org.eclipse.platform:org.eclipse.core.runtime:3.13.0'
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.google.guava') {
                details.useVersion '21.0'
            }
            if (details.requested.group == 'org.eclipse.jdt' && details.requested.name == 'org.eclipse.jdt.core') {
                details.useVersion('3.13.102')
            }
        }
    }
}

mainClassName = 'sample.Main'
